# SAS的IML模块

## 1. IML内容概述

iml Interactive Matrix Language

iml 可以直接读取外部文件，然后转化为矩阵运算，或者读入data步数据集文件，转化为矩阵运算



## 2. 矩阵的创建

### 2.1直接输入法创建

```SAS
proc iml;
	a1 = 56;
	a2 = .;
	a3 = 'bios111';
	a4 = 'BIOS111';
	print a1 a2 a3 a4;
run;

proc iml;
	a1 = {56};
	a2 = {.};
	a3 = {'bios111'};
	a4 = {'BIOS111'};
	print a1 a2 a3 a4;
run;
```

当矩阵包含多个元素时，要用大括号`{}`把等号右边的元素括起来，并用`,`来分隔矩阵的行

```SAS
proc iml;
	a = {1 2 3 4 5 6};
	b = {1,2,3,4,5,6};
	c = {1 2 3, 4 5 6};
	print a b c;
quit;

```



### 2.2 利用IML中的函数来产生矩阵

1. `block` 函数 创建对角矩阵
   此函数的功能是根据给定的矩阵（必须是方阵）来产生一个对角块矩阵

   ```sas
   proc iml;
   	A = block(5,6);
   	B = block(5,6,7);
   	C = {1 1,3 3};
   	D = {5 5,7 7};
   	E = block(C,D);
   	print A B C D E;
   quit;
   
   ```

   

2. `I` 函数，创建单位矩阵
   产生单位阵，`I(dimension)` 里面数字代表方阵的阶数

   ```sas
   * I函数;
   proc iml;
   	A = I(3);
   	B = i(2.5);
   	print A B;
   quit;
   ```

   

3. `J` 函数   根据给定的行数和列数重复产生矩阵
   `J(nrow,ncol,value)`如果没有指定value的值，系统默认值为1

   ```sas
   proc iml;
   	A = j(4,3,2);
   	B = j(4,3,'tom');
   	C = j(4,3);
   	D = j(4);
   	print A B C D;
   quit;
   ```

   

4. `do`函数
   此函数功能是产生一个序列 `do(start, stop, increment)`

   ```sas
   * do 函数;
   proc iml;
   	A = do(1,13,3);
   	B = do(3,-3,-2);
   	print A B;
   quit;
   
   ```

   

### 2.3 在IML的环境中把外部文件或SAS数据集转化为矩阵

略......

## 3. 矩阵的运算

| 运算符 | 功能                           |
| ------ | ------------------------------ |
| + -    | 矩阵的每个元素的加减运算       |
| :      | 产生行向量，例如1:5            |
| *      | 两个矩阵相乘                   |
| #      | 两个矩阵对应元素相乘           |
| **     | 对应矩阵的乘方                 |
| ##     | 矩阵中的每个元素的乘方         |
| /      | 矩阵的除法，被除数必须为一常数 |
| \|\|   | 两个矩阵的水平连接             |
| //     | 两个矩阵的竖直连接             |
| <>     | 取两个矩阵中的最大元素         |
| ><     | 取两个矩阵的最小元素           |
| &      | 与                             |
| \|     | 或                             |
| ^      | 非                             |
| <      | 小于                           |
| >      | 大于                           |
| =      | 等于                           |
| >=     | 大于或者等于                   |
| <=     | 小于或等于                     |
| ^=     | 不等于                         |
| `      | 转置                           |



```sas
proc iml;
	A = {3 4, 5 6};
	B = {7 8, 1 2};
	C = A+B;
	D = A#B;
	E = A`;
	F = A||B;
	G = A//B;
	H = A*B;
	print A B C D E F G H;
quit;
```



## 4. 矩阵下标的使用

在IML中，下标的选用在矩阵的运算中占有非常重要的地位，它的使用更能体现IML语言的灵活与便捷

下标在使用中需要置于括号`[]`中

格式`matrix[row,column]` row为行下标，一般为数字或者表达式，可以选择矩阵的行，column 为列下标，可以选择矩阵的列

### 1. 选中矩阵的单个元素

使用行、列下标定位

`A = {4 5, 6 7, 8 9}; B = A[2,1];` 表示B取得A的第二行第一列的元素

 只使用一个下标定位

`A = {4 5, 6 7, 8 9}; B = A[5]; `  表示B取矩阵A中从左到右数到第5个元素，也就是8 

### 2. 选中矩阵的整行元素

在选定已知矩阵的某一整行元素时，只指定格式中行数的值，而不指定列数的值，表示选择指定行中的所有元素

`A = {4 5, 6 7, 8 9}; B = A[2,];`  表示取矩阵A的第二行所有元素

### 3. 选中矩阵的整列元素

在选定已知矩阵的某一整行元素时，只指定格式中列数的值，而不指定行值

`A = {4 5, 6 7, 8 9}; B = A[,2];`

### 4. 选中矩阵的子矩阵

选定矩阵的子矩阵是指运用下标的功能从已知的矩阵中选择一部分元素，使这些元素重新组成一个矩阵

```sas
proc iml;
	A = {4 5 6,6 7 8,7 8 9};
	B = A[{1 2},{2 3}];  /* B表示取矩阵A的第1，2行和第2，3列交叉点上的元素重新排列成矩阵 */
	C = A[,{2,3}];  /* C表示取矩阵A的第2列与第3列所有元素所得的矩阵 */
	D = A[1:2,1:3]; /* D表示取矩阵A的第一二行与矩阵的第一二三列交叉点上的元素所组成的矩阵 */
print A B C D;
quit;
```

### 5. 改变矩阵的内容

可以运用下标来修改已知矩阵中的单个元素、整行、整列元素或子矩阵

```SAS
* 修改矩阵元素;
proc iml;
	A  = {1 2 3,4 5 6, 7 8 9};
	print A;
	A[2,3] = 1;  /* 表示把矩阵A中第二行第三列的元素6改为1 */
	print A;
	A[1,] = {1 1 1}; /* 表示把矩阵A第一行元素全部改为1 */
	print A;
quit;

```



矩阵下标还可以用来进行矩阵的计算，常见的矩阵运算符有8种

| 运算符 | 功能           |
| ------ | -------------- |
| +      | 加             |
| #      | 乘             |
| <>     | 取最大         |
| ><     | 取最小         |
| <:>    | 取最大值的位置 |
| >:<    | 取最小值的位置 |
| :      | 取均值         |
| ##     | 取平方和       |



```sas
proc iml;
	a = {1 4 2, 9 8 5, 0 1 3};
	b = a[+,{1,2}]; *对矩阵a的第一列和第二列分别求和;
	c = a[+,<>];    *对矩阵a的每一列求和并取最大值;
	d = a[<>,]; *取矩阵a每一列的最大值;
	e = a[,<:>]; *取矩阵a中行的最大值的位置;
	f = a[+,+]; * 取矩阵a中所有元素之和;
	g = a[:,]; * 取矩阵a每一列的均值;
	h = a[##,]; * 取矩阵每一列的平方和;
	i = a[<>]; * 取矩阵a中的最大值;
	j = a[<:>]; * 取矩阵a中最大值的位置;
	print a b c d e f g h i j;
quit;
```







## 5. IML程序设计语言

IML具有特定的程序设计环境，包含许多其他SAS模块所不具有的特点

在IML环境下，可以通过特定语句来控制整个程序的执行

### 5.1 `IF -THEN/ELSE`语句

> if 表达式 then 语句1;
>
> ​	else 语句2;

在`if-then/else`语句中，可以使用嵌套的形式来进行多次假设

### 5.2 模块的定义与实施

在IML中，模块(modules)是一个新的名词，主要用于：在一个完成的程序中，创建一组语句，使之作为一个单位，方便其他语句的调用。更确切地说，就是在IML中产生一个子程序或函数。

+ 在IML中，创建一个单独的编程环境，即产生一个模块。在这个模块中定义变量，使这些变量的功能只限于这个模块。

+ 模块也被称为函数或子程序，它以`start`语句开始，以`finish`语句结束

+ 当模块里只有一个参数时，就被称为函数（不同于IML自身函数，模块中的函数为自定义函数），可以像IML自身的函数一样被调用

+ 当模块里的参数超过一个时，就被称为子程序

+ 在IML中，当调用一个函数时，直接指定这个函数的名字即可；

+ 当调用子程序时，就必须通过`call`语句或者`run`语句才能实现；

```SAS
proc iml;
	start addOne;  
	a= a+1;
	finish addOne;
	
	a = 100;
	call addOne;
	print a;
quit;
```





### 5.3 `DO...END`语句



### 5.4 跳转`JUMPING`语句



### 5.5 停止语句











